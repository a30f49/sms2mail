plugins {
    alias(libs.plugins.android.application)
}

android {
    namespace 'com.example.sms2mail'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.sms2mail"
        minSdk 27
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // 签名配置
    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("signing.properties")
            def keystoreProperties = new Properties()
            if (keystorePropertiesFile.exists()) {
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            }
            
            keyAlias project.findProperty('KEY_ALIAS') ?: 'sms2mail-key'
            keyPassword keystoreProperties['KEYSTORE_PASSWORD'] ?: System.getenv('KEYSTORE_PASSWORD')
            storeFile file('../' + (project.findProperty('KEYSTORE_FILE') ?: 'sms2mail-release.keystore'))
            storePassword keystoreProperties['KEYSTORE_PASSWORD'] ?: System.getenv('KEYSTORE_PASSWORD')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packaging {
        resources {
            excludes += 'META-INF/NOTICE.md'       // This line fixes your specific error
            excludes += 'META-INF/LICENSE.md'       // This line fixes your specific error
        }
        dex {
            useLegacyPackaging = false
        }
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

//    implementation 'androidx.appcompat:appcompat:1.6.1'
//    implementation 'com.google.android.material:material:1.9.0'
//    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // JavaMail API for Android
    implementation 'com.sun.mail:android-mail:1.6.7'
    implementation 'com.sun.mail:android-activation:1.6.7'

}